// Dean Cureton, https://github.com/deancureton/Sigma/blob/main/Sigma/Grammar/grammar.bnf
program ::= [statementList]
statementList ::= statement+
statement ::= variableDeclaration
            | variableInitialization
            | ( assignment "‼️" )
            | functionDefinition
            | forLoop
            | foreachLoop
            | whenLoop
            | loopLoop
            | ifStatement
            | comment
            | ( expression "‼️" )

variableDeclaration ::= type ID "‼️"
variableInitialization ::= strictType regularAssignment "‼️"

assignment ::= unaryAssignment | regularAssignment

functionDefinition ::= "func" ID "<-" functionArguments block "‼️"
functionCall ::= ID "{" callArguments "}" "‼️"
callArguments ::= primary*

forLoop ::= "for" "{" variableInitialization "‼️" expression "‼️" assignment "}" block
foreachLoop ::= "for" "{" "var" ID "of" ID "}" block
whenLoop ::= "when" "{" expression "}" block
loopLoop ::= "loop" "{" number "}" block

ifStatement ::= "if" "{" expression "}" block butifStatement* [butStatement]
butifStatement ::= "butif" "{" expression "}" block
butStatement ::= "butif" "{" expression "}" block

comment ::= singleLineComment | multiLineComment
singleLineComment ::= "\\" COMMENTTEXT
multiLineComment ::= "\\." COMMENTTEXT ".\\"

type ::= "var" | strictType
strictType ::= "str" | "num" | "tf" | "arr"

unaryAssignment ::= increment | decrement
increment ::= ID "++"
decrement ::= ID "--"

regularAssignment ::= ID [binaryArithmeticOperator] "<-" expression

functionArguments ::= regularFunctionArgument* optionalFunctionArgument*
regularFunctionArgument ::= type ID
optionalFunctionArgument ::= "[" type ID "]"

block ::= "|" [statementList] "|"

expression ::= binaryExpression
             | primary
             | unaryExpression

binaryExpression ::= primary binaryOperator primary
                   | primary binaryOperator binaryExpression

primary ::= number | ID | STRING | boolean | unaryExpression | array | cast | functionCall

unaryExpression ::= unaryAssignment | negationExpression | minusExpression | parenthesizedExpression
parenthesizedExpression ::= "{" expression "}"
negationExpression ::= "!" expression
minusExpression ::= "-" expression

binaryOperator ::= binaryArithmeticOperator | binaryComparator | binaryBooleanOperator
binaryArithmeticOperator ::= "+" | "-" | "/" | "*" | "//" | "^" | "%"
binaryComparator ::= ">" | "<" | ">?" | "<?" | "≥" | "≤" | "?" | "!?" | "??" | "!??" | "~" | "!~"
binaryBooleanOperator ::= "and" | "or" | "not" | "nand" | "nor" | "xor" | "xnor" | "implies"

number ::= INTEGER | REAL
boolean ::= "true" | "false"

array ::= "(" arrayElements ")"
arrayElements ::= primary*

cast ::= ID "." strictType